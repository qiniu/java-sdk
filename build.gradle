import java.util.regex.Matcher

apply plugin: 'java'

sourceCompatibility = 1.6
targetCompatibility = 1.6
version = '1.0'
[compileJava,compileTestJava,javadoc]*.options*.encoding = 'UTF-8'

repositories {
    mavenCentral()
}

dependencies {
    String version = System.getProperty("java.version")
    println("JDK: " + version)
    int c = version.compareTo("1.7")
    if (c < 0) {
        compile fileTree(dir: 'libs', include: '*.jar')
    } else {
        compile group:'com.squareup.okhttp', name:'okhttp', version:'2.7.1'
    }
    compile group:'com.google.code.gson', name:'gson', version:'2.3.1'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

apply plugin: 'checkstyle'


def versionName() {
    String config = 'src/main/java/com/qiniu/common/Config.java'
    String fileContents = new File(config).text
    Matcher myMatcher = fileContents =~ /VERSION = "(.+)";/
    String version = myMatcher[0][1]
    println(version)
    return version
}

def versionNameToCode(String version) {
    String v = version.replaceAll(/\./, '')
    return v.toLong()
}

String version = versionName()
int code = versionNameToCode(version)

setProperty('VERSION_NAME', version)
setProperty('VERSION_CODE', code)

apply from: 'mvn_push.gradle'

apply plugin: 'eclipse'

task gen_eclipse(dependsOn:[
'cleanEclipseProject', 'cleanEclipseClasspath',
'eclipseProject', 'eclipseClasspath'])
eclipseProject.mustRunAfter cleanEclipseProject
eclipseClasspath.mustRunAfter cleanEclipseClasspath